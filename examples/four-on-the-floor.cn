bpm 124
note 58  # A#3

let en bpm * 8  # Eight Notes
let qn bpm * 4  # Quarter Notes
let hn bpm * 2  # Half Notes
let fn bpm * 1  # Full Notes

let n note  # Shorthand

let o1 n + 12 * 0
let o2 n + 12 * 1
let o3 n + 12 * 2

# MIDI Channels
alias c_bd 1  # Bass Drum
alias c_sn 2  # Snare Drum
alias c_oh 3  # Open HiHat
alias c_ch 4  # Closed HiHat
alias c_cb 5  # Cowbell
alias c_gb 6  # Gritty Bass
alias c_sh 7  # Shaker
alias c_bs 8  # Bass

# MIDI Base Notes
let bd 69  # Bass Drum Base Note
let sn 69  # Snare Drum Base Note
let oh 69  # Open HiHat Base Note
let ch 69  # Closed HiHat Base Note
let cb 69  # Cowbell Base Note
let sh 1   # Shaker Base Note

# Bars
# seconds = 60 / bpm * steps
4:16        @ qn ** 4 => b_bd  # !...!...!...!... (x4=64)  @ 496bpm = 7.74s
2:16   > 4  @ qn ** 4 => b_sn  # ....!.......!... (x4=64)  @ 496bpm = 7.74s
4:16   > 2  @ qn ** 4 => b_ch  # ..!...!...!...!. (x4=64)  @ 496bpm = 7.74s
1:16   > 3  @ qn ** 4 => b_cb  # ...!............ (x4=64)  @ 496bpm = 7.74s
16:128 > 10 @ en ** 1 => b_sh  # ..!.......!..... (x8=128) @ 992bpm = 7.74s

!..! ..!. ..!. ..!! @ qn ** 4 => b_bs  # !..!..!...!...!! (x4=64) @ 496bpm = 7.74s
20:64               @ qn ** 1 => b_gb  # !...!..!..!..!.. (x4=64) @ 496bpm = 7.74s

  send c_bd b_bd map bd
$ send c_sn b_sn map sn
$ send c_ch b_ch map ch
$ send c_cb b_cb map cb
$ send c_sh b_sh map sh

$ send c_bs b_bs map
	o1 - 4  o2 - 4  o1 - 4  o2 - 4
	o1 - 7  o2 - 7  o1 - 7  o2 - 7
	o1 - 5  o2 - 5  o1 - 5  o2 - 5
	o1      o2      o1      o2

$ send c_gb b_gb map
	o1 + 3  o1 + 3  o1 + 3  o1 + 5
	o1 + 3  o1 + 5  o1 + 5  o1 + 8
	o1 + 5  o1 + 8  o1 + 8  o1
	o1 + 8  o1      o1      o1

