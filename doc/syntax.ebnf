literal ::= ? [0-9_]+ ? | ? 0x[0-9A-z_]+ ? | ? 0b[0-1_]+ ?
identifier ::= ? \S+ ?
comment ::= ? #.+$ ?

// `midi 5 @ 240`
midi ::= 'midi' <literal> '@' <literal>

// `4/8+1` `2/5`
euclidean ::= <literal> '/' <literal> [ '+' <literal> ]
// `5/9 => snare => midi 3 @ 120`
chain ::= <expr> '=>' ( <identifier> | <midi> )

step ::= '!' | '.'
// `[!...!...]`
sequence ::= '[' step* ']'

// `2/8 | !>>2/8`
infix ::= <expr> ( '|' | '&' | '^' | ',' ) <expr>
prefix ::= ( '~' | '<' | '>' ) <expr>
postfix ::= <expr> ( '<<' | '>>' | '*' ) <literal>

// `[!..!..] .. 2/6 => hihat => midi 1 @ 360`
expr ::= <sequence> | <infix> | <prefix> | <postfix> | <chain> | '(' <expr> ')'

program ::= <expr>*
