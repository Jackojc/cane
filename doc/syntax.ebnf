literal ::= ? [0-9_]+ ? | ? 0x[0-9A-z_]+ ? | ? 0b[0-1_]+ ?
identifier ::= ? \S+ ?
comment ::= ? #.+$ ?

sequence ::= ( '!' | '.' )*
euclidean ::= <literal> '/' <literal>

infix_e ::= ( '|' | '&' | '^' | ',' | "sync" | "fit" ) <expr>
infix_l ::= ( '<<' | '>>' | '*' | '@' ) <literal>
infix ::= <expr> ( <infix_e> | <infix_l> )

prefix ::= ( '~' | '\'' ) <expr>
postfix ::= <expr> ( '<' | '>' | '?' )

chain ::= <expr> '=>' <identifier>

expr ::= <identifier> | <sequence> | <prefix> | <infix> | <postfix> | <chain> | '(' <expr> ')'

sink ::= <expr> ( '~>' ( <literal> | <identifier> ) )*
alias ::= "alias" <identifier> '~>' <literal>

stat ::= ( <sink> | <alias> | "clear" ) *

program ::= <stat>*
