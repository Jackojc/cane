literal ::= ? [0-9_]+ ?
identifier ::= ? \S+ ?
comment ::= ? #.+$ ?


sequence ::= ( '!' | '.' )*
euclidean ::= ( ':' <lit_expr> | <literal> ) ':' <lit_expr>

channel ::= ( <literal> | <identifier> )


lit_infix ::= <lit_expr> ( '+' | '-' | '*' | '/' ) <lit_expr>
lit_prefix ::= ( "len" | "beats" | "skips" ) <seq_expr>

lit_expr ::=
	"bpm" | "note" | <identifier> |
	<lit_prefix> | <lit_infix> |
	'(' <lit_expr> ')'


seq_prefix ::= ( '~' | '\'' ) <seq_expr>
seq_prefix ::= "send" <channel> <seq_expr>

seq_infix_expr ::= <seq_expr> ( '|' | '&' | '^' | ',' ) <seq_expr>
seq_infix_lit  ::= <seq_expr> ( '<' | '>' | '**' | '@' ) <lit_expr>
seq_infix_lit  ::= <seq_expr> "map" <lit_expr>+
seq_infix_lit  ::= <seq_expr> "vel" <lit_expr>+
seq_infix_lit  ::= <seq_expr> '=>' <identifier>
seq_infix      ::= <seq_infix_expr> | <seq_infix_lit>

seq_postfix ::= <seq_expr> ( '?' | "car" | "cdr" )

seq_expr ::=
	<identifier> | <sequence> |
	<seq_prefix> | <seq_infix> | <seq_postfix> |
	'(' <seq_expr> ')'


let   ::= "let" ( <identifier> <lit_expr> )+
alias ::= "alias" ( <identifier> <literal> )+
bpm   ::= "bpm" <lit_expr>
note  ::= "note" <lit_expr>

stat ::= <let> | <alias> | <bpm> | <note> | <seq_expr>

program ::= <stat>*
